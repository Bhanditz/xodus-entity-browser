buildscript {
    ext.xodusVersion = '1.2.3010'
    ext.kotlinVersion = '1.2.21'
    ext.buildVersion = '1.1.0'

    repositories {
        mavenCentral()
        maven { url 'http://repository.jetbrains.com/xodus' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id 'java'
    id 'idea'                                         // ide sync fine tuning
    id "com.jfrog.bintray" version "1.7"
    id 'com.github.johnrengelman.shadow' version '2.0.2'
}

apply from: 'libraries.gradle'
apply plugin: 'kotlin'

ext.globalArtifactsBuildDir = buildDir
group = 'com.lehvolk.xodus'

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'
    sourceCompatibility = 1.8
    version = buildVersion

    repositories {
        mavenCentral()
        maven { url "https://dl.bintray.com/lehvolk/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
        maven { url 'http://repository.jetbrains.com/xodus' }
    }

    idea {
        module {
            downloadSources = true
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getProperty('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getProperty('BINTRAY_API_KEY')
    publications = ['mavenBintray']

    pkg {
        repo = 'maven'
        name = 'public'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/lehvolk/xodus-entity-browser.git'
        labels = ['xodus']
        publicDownloadNumbers = true
        publish = true
        version {
            name = buildVersion
            desc = 'Xodus entity browser'
        }
    }
}

publishing {
    publications {
        mavenBintray(MavenPublication) {
            artifact "build/xodus-entity-browser-${buildVersion}.zip"

            groupId 'com.lehvolk.xodus'
            artifactId 'xodus-entity-browser'
            version buildVersion
        }
    }
}

defaultTasks 'clean build'

repositories {
    mavenCentral()
}