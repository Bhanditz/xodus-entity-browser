buildscript {
    ext.xodusBuildTypeId = 'Xodus_Build'
    ext.xodusMavenVersion = '1.1.0'
    ext.kotlinVersion = '1.2.0'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'org.akhikhl.gretty:gretty:+'
    }
}

plugins {
    id 'java'
    id 'idea'                                         // ide sync fine tuning
    id 'com.github.jk1.tcdeps' version '0.13'        // resolves dependencies from TeamCity
    id "com.jfrog.bintray" version "1.7"
}

apply from: 'libraries.gradle'
apply plugin: 'kotlin'

ext.globalArtifactsBuildDir = buildDir

def isMavenXodus = Boolean.getBoolean('xodus-from-maven')
ext.isMavenXodus = isMavenXodus

def buildVersion = "1.0.0-" + (new Date().format('yyyyMMdd'))

group = 'com.lehvolk.xodus'
version = buildVersion

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'com.github.jk1.tcdeps'
    apply plugin: 'maven'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        teamcityServer { url = 'https://teamcity.jetbrains.com' }
        maven { url "https://dl.bintray.com/lehvolk/maven" }
        maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    }

    idea {
        module {
            downloadSources = true
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getProperty('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getProperty('BINTRAY_API_KEY')
    publications = ['mavenBintray']

    pkg {
        repo = 'maven'
        name = isMavenXodus ? 'public' : 'jetbrains-team'
        userOrg = user
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/lehvolk/xodus-entity-browser.git'
        labels = ['xodus']
        publicDownloadNumbers = true
        publish = true
        version {
            name = buildVersion
            desc = 'Xodus entity browser'
        }
    }
}

publishing {
    publications {
        mavenBintray(MavenPublication) {
            artifact 'build/entity-browser-launcher.zip'

            groupId 'com.lehvolk.xodus'
            artifactId 'entity-browser-launcher'
            version buildVersion
        }
    }
}

defaultTasks 'clean build'

repositories {
    mavenCentral()
}